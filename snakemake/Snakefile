
configfile: "config.json"

def get_tree_ids():
    import pandas as pd
    df = pd.read_excel(config["TREE_IDS_SOURCE_FILE"])
    return list(df.TREE_ID)

TREE_ID = get_tree_ids()

rule all:
    input:
        expand("{base_path}/output/fine_segmentation/{base_name}_{tree_id}.laz", base_path=config["BASE_PATH"], base_name=config["BASE_NAME"], tree_id=TREE_ID)
    
rule add_parameters_and_normalize:
    input:
        "{BASE_PATH}/{BASE_NAME}.laz"
    output:
        "{BASE_PATH}/output/{BASE_NAME}_normalized.las"
    script:
        "../workflow/01/01_add_parameters_and_normalize.py"

rule georeference:
    input:
        rules.add_parameters_and_normalize.output
    output:
        "{BASE_PATH}/output/{BASE_NAME}_georef.las"
    script:
        "../workflow/02/02_georeference.py"

rule spatial_resample:
    input:
        rules.georeference.output
    output:
        "{BASE_PATH}/output/{BASE_NAME}_resampled.las"
    script:
        "../workflow/03/03_spatial_resample.py"

rule clipping_trees:
    params:
        output_dir = "{BASE_PATH}/output/single_trees/"
    input:
        rules.georeference.output
    output:
        expand("{{BASE_PATH}}/output/single_trees/{{BASE_NAME}}_{TREE_ID}.laz", TREE_ID=TREE_ID)
    script:
        "../workflow/04/04_clipping_trees.py"

rule normalize_to_ground:
    params:
        input_dir = "{BASE_PATH}/output/single_trees/",
        output_dir = "{BASE_PATH}/output/single_trees_normalized_to_ground/"
    input:
        rules.clipping_trees.output
    output:
        expand("{{BASE_PATH}}/output/single_trees_normalized_to_ground/{{BASE_NAME}}_{TREE_ID}.laz", TREE_ID=TREE_ID)
    script:
        "../workflow/05/05_normalize_to_ground.py"

rule fine_segmentation:
    params:
        input_dir = "{BASE_PATH}/output/single_trees_normalized_to_ground/",
        output_dir = "{BASE_PATH}/output/fine_segmentation/",
        output_dir_noise = "{BASE_PATH}/output/fine_segmentation_noise/"
    input:
        rules.normalize_to_ground.output
    output:
        expand("{{BASE_PATH}}/output/fine_segmentation/{{BASE_NAME}}_{TREE_ID}.laz", TREE_ID=TREE_ID),
        expand("{{BASE_PATH}}/output/fine_segmentation_noise/{{BASE_NAME}}_{TREE_ID}.laz", TREE_ID=TREE_ID)
    script:
        "../workflow/06/06_fine_segmentation.py"
